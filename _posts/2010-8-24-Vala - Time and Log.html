---
title: Vala - Time and Log
category: programming
old-category: C/C++/C#/Java
tags: [vala]
permalink: /archives/13543703.html
---
<p>
This article describes two things about <a href="http://en.wikipedia.org/wiki/Vala_%28programming_language%29">Vala</a> programming. First, how to get current time and output it as a formatted timesamp. Second, how to design custom log.
</p>

<!--more-->
<h5>
What Time Is It?
</h5>
<p>
Vala provides two way to get time (it represents the  number  of  seconds  elapsed since 00:00:00 on January 1, 1970, UTC). They are <dfn><a href="http://valadoc.org/glib-2.0/GLib.TimeVal.html">TimeVal</a></dfn> and <dfn><a href="http://valadoc.org/glib-2.0/time_t.html">time_t</a></dfn>.
<dfn>time_t</dfn> represents time by seconds. <dfn>TimeVal</dfn> represents more precise time, with seconds and microseconds.
</p>
<p>
<dfn>time_t</dfn> does not provide any methods to format as string. We should use <dfn><a href="http://valadoc.org/glib-2.0/GLib.Time.html">Time</a></dfn> to do this.
</p>
<p>
The following is an example to get current time and to output it as a formatted timestamp.
</p>

{% highlight c# %}
void main() {
    TimeVal nowv = TimeVal();
    nowv.get_current_time();
    stdout.printf("It is %s.\n", nowv.to_iso8601());

    // man ctime
    Time now1 = Time();
    now1 = Time.local( time_t() ); // get current time.
    stdout.printf("It is %s.\n", now1.to_string());

    // man strftime
    stdout.printf("It is %s.\n", now1.format("%F %H:%M:%S"));

    time_t t = nowv.tv_sec; // get time from TimeVal.
    now1 = Time.local( t );
    stdout.printf("It is %s.\n", now1.to_string());
}
{% endhighlight %}


<pre><code class="language-term">
rock@desktop:~$ valac what_time_is_it.vala
rock@desktop:~$ ./what_time_is_it
It is 2010-08-24T08:10:18.484744Z.
It is 2010-08-24 16:10:18.
It is 2010-08-24 16:10:18.
It is 2010-08-24 16:10:18.

</code></pre>

<h5>
Custom Your Log
</h5>
<p>
When we design a background daemon, we usually need to log some informations to file. The simple way is just redirect stdout (or stderr) to our log file, then we could simply log our informations via printf or puts.
</p>
<p>
The following is a custom log example. It also adds a timestamp (see above) to log messages.
</p>


{% highlight c# %}
#if 0
namespace Log {
    void puts(string msg) {
        Time now = Time();
        now = Time.local(time_t()); 
        stderr.printf("[%s] %s\n", now.to_string(), msg);
        stderr.flush();
    }
}
#endif

public class Log {
    public static void puts(string msg) {
        Time now = Time();
        now = Time.local(time_t()); 
        stdout.printf("[%s] %s\n", now.to_string(), msg);
        stdout.flush();
    }
}

void main() {
    //redirect stdout to /tmp/test.log
    stdout = FileStream.open("/tmp/test.log", "ab");
    
    var s = "world";
    Log.puts(@"error message about $s.");
    
    var i = 10;
    var j = "jack";
    Log.puts(@"i is $i, j is $j.");
}
{% endhighlight %}


<pre><code class="language-term">
rock@rock-desktop:~$ ./time_log 
rock@rock-desktop:~$ cat /tmp/test.log 
[2010-08-24 16:26:01] error message about world.
[2010-08-24 16:26:01] i is 10, j is jack.

</code></pre>

<h5>
How about GLib.log
</h5>

<p>
By default way, GLib.log(), GLib.message(), GLib.error(), GLib.warning(), and etc, will directly output message to /dev/stderr. We could not use FileStream to redirect those. You may invoke <a href="http://valadoc.org/glib-2.0/GLib.Log.set_default_handler.html">GLib.Log.set_default_handler()</a> or  GLib.Log.set_handler() to change handler.
</p>


{% highlight c# %}
void main() {
    var log_stream = FileStream.open("/tmp/test2.log", "ab");
    
    Log.set_default_handler(
        (domain, level, message) => {
            Time now = Time();
            now = Time.local(time_t()); 
            log_stream.printf("[%s] %s\n", now.to_string(), message);
        }
    );
    message("%s", "hello");
}
{% endhighlight %}


<pre><code class="language-term">
rock@rock-desktop:~$ ./time_log2 
rock@rock-desktop:~$ cat /tmp/test2.log 
[2010-08-24 16:48:27] time_log2.vala:11: hello

</code></pre><div class="note">樂多舊網址: http://blog.roodo.com/rocksaying/archives/13543703.html</div>